plugins {
    id 'java'
    id 'maven-publish'
    id "io.github.goooler.shadow" version "8.1.8"
}

group 'club.minnced'
version '2.0.2'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        url "https://nexus.web.cern.ch/nexus/content/repositories/public/"
    }
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url "https://maven.scijava.org/content/repositories/public/"
    }
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.14.0'
    implementation 'club.minnced:discord-rpc-release:v3.4.0'
    implementation 'org.jetbrains:annotations:24.1.0'
}

tasks.withType(JavaCompile).configureEach {
    options.incremental = true
    options.encoding = 'UTF-8'
}

java {
    withSourcesJar()
    withJavadocJar()
}

// Publishing
jar {
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Target-Platforms': 'win32-x86-64, win32-x86, linux-x86-64, darwin'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
        }
    }
}

build {
    dependsOn shadowJar
}
